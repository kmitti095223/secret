Option Explicit

Sub MoveActiveXTextBoxesToCells()
    Dim ws As Worksheet
    Dim obj As OLEObject
    Dim cell As Range
    Dim bDelete As Boolean
    Dim lastCol As Long
    Dim lastRow As Long
    Dim c As Long
    Dim r As Long
    
    ' アクティブシートを設定
    Set ws = ActiveSheet
    
    ' グリッドラインを非表示にする
    ws.Activate
    ActiveWindow.DisplayGridlines = False
    
    ' ワークシート上のOLEオブジェクトをループ
    For Each obj In ws.OLEObjects
        bDelete = False
        
        ' OLEオブジェクトが配置されているセルを取得
        Set cell = ws.Cells(obj.TopLeftCell.Row, obj.TopLeftCell.Column)
        
        Select Case TypeName(obj.Object)
        Case "HTMLText"
            ' テキストボックスの内容をセルにコピー
            cell.Value = obj.Object.Value
            If cell.Column > lastCol Then lastCol = cell.Column
            If cell.Row > lastRow Then lastRow = cell.Row
            bDelete = True
            
        Case "HTMLCheckbox"
            ' チェックボックスの状態を文字列に変換（ON=?、OFF=?）
            cell.Value = IIf(obj.Object.Checked, ChrW(&H2611), ChrW(&H2610))
            If cell.Column > lastCol Then lastCol = cell.Column
            If cell.Row > lastRow Then lastRow = cell.Row
            bDelete = True
        End Select
        
        ' チェックボックスやテキストボックスを削除
        If bDelete Then
            obj.Delete
        End If
        
    Next obj
    
    ' 列の幅を調整
    For c = 1 To lastCol
        ws.Columns(c).ColumnWidth = 99
        ws.Columns(c).AutoFit
    Next
    
    ' 行の高さを調整
    For r = 1 To lastRow
        ws.Rows(r).AutoFit
    Next
End Sub


